# __source__ references the current object
# __subject__ references the current value

# The majority of values are converted from float to integer by multiplying
# by 100 and truncating. Notable exceptions are Axis and Cct, which require
# no conversion.

### extensions

using extension auth;

module default {

### globals

global cur_surgeon := (assert_single(
    (select Surgeon filter .identity = global ext::auth::ClientTokenIdentity)
));

### scalars

    scalar type Acd extending int32 { constraint min_value(0); constraint max_value(600); }
    scalar type Adverse extending enum<Rhexis, Pc, Zonule, Other>;
    scalar type Al extending int32 { constraint min_value(1200); constraint max_value(3800); }
    scalar type Axis extending int32 { constraint min_value(0); constraint max_value(179); }
    scalar type Cct extending int32 { constraint min_value(350); constraint max_value(650); }

    scalar type EmailType extending str {
        # HTML5 allows dotless domains, but ICANN doesn't, so this regex requires the dot.
        constraint regexp("^[a-zA-Z0-9.!#$%&'*+/=?^_`{|}~-]+@[a-zA-Z0-9](?:[a-zA-Z0-9-]{0,61}[a-zA-Z0-9])?(?:\\.[a-zA-Z0-9](?:[a-zA-Z0-9-]{0,61}[a-zA-Z0-9])?)+$");
    }

    scalar type Focus extending enum<Mono, Edof, Multi>;

    scalar type Formula extending enum<
        AscrsKrs,
        Barrett,
        BarrettTrueK,
        Evo,
        Haigis,
        HaigisL,
        HillRbf,
        HofferQ,
        Holladay1,
        Holladay2,
        Kane,
        Okulix,
        Olsen,
        SrkT,
        Other
    >;

    scalar type Lt extending int32 { constraint min_value(200); constraint max_value(800); }
    scalar type Main extending int32 { constraint min_value(100); constraint max_value(600); }
    scalar type Side extending enum<Right, Left>;
    scalar type Wtw extending int32 { constraint min_value(800); constraint max_value(1400); }

### abstract objects

    abstract type Cyl extending SoftCreate {
        required power: int32;
        required axis: Axis;
    }

    abstract type SoftCreate {
        required created_at: datetime {
            default := datetime_current();
            readonly := true;
        }
    }

### objects

    type Biometry extending SoftCreate {
        required al: Al;
        required flat_k: K;
        required steep_k: K;
        required acd: Acd;
        required lt: Lt;
        cct: Cct;
        wtw: Wtw;
    }

    type AfterVa extending SoftCreate {
        best: Va;
        required raw: Va;
    }
    
    type BeforeVa extending SoftCreate {
        required best: Va;
        raw: Va;
    }

    # case is a reserved keyword
    # todo: add sex here and in Rust types
    type Cas extending SoftCreate {
        required side: Side;
        biometry: Biometry;
        required target: Target;

        required year: int32 {
            default := <int32>datetime_get(datetime_current(), "year");
            constraint min_value(2000);
            constraint max_value(2100);
        }

        sia: OpSia;
        opiol: OpIol;
        adverse: Adverse;
        required va: OpVa;
        required refraction: OpRefraction;
    }

    type Iol extending SoftCreate {
        required model: str { constraint exclusive; }
        name: str;
        company: str;
        required focus: Focus { default := Focus.Mono; }
        required toric: bool { default := false; }
    }

    type IolCyl extending Cyl {
        constraint expression on (.power >= 100 and .power <= 2000 and .power % 25 = 0);
    }

    type K extending Cyl {
        constraint expression on (.power >= 3000 and .power <= 6500);
    }

    type OpSia extending SoftCreate {
        required main: Main;
        required sia: Sia;
    }

    type OpIol extending SoftCreate {
        required iol: Iol;

        required se: int32 { 
            constraint min_value(-2000);
            constraint max_value(6000);
            constraint expression on (__subject__ % 25 = 0);
        }

        cyl: IolCyl;
    }

    type OpRefraction extending SoftCreate {
        required before: Refraction;
        required after: Refraction;
    }
    
    type OpVa extending SoftCreate {
        required before: BeforeVa;
        required after: AfterVa;
    }

    type Refraction extending SoftCreate {
        required sph: int32 { 
            constraint min_value(-2000);
            constraint max_value(2000);
            constraint expression on (__subject__ % 25 = 0);
        }

        cyl: RefractionCyl;
    }

    type RefractionCyl extending Cyl {
        constraint expression on (.power >= -1000 and .power <= 1000 and .power % 25 = 0);
    }

    type Sia extending Cyl {
        constraint expression on (.power >= 0 and .power <= 200);
    }

    type Site extending SoftCreate {
        required name: str { constraint exclusive; }
    }

    type Surgeon extending SoftCreate {
        required identity: ext::auth::Identity { constraint exclusive; }
        required email: EmailType { constraint exclusive; }
        terms: datetime; # the date terms were agreed by the surgeon
        first_name: str;
        last_name: str;
        defaults: SurgeonDefaults;
        sia: SurgeonSia;
        multi cases := .<surgeon[is SurgeonCas];
    }

    type SurgeonCas extending SoftCreate {
        required surgeon: Surgeon;
        required urn: str;
        required date: cal::local_date;
        required cas: Cas { constraint exclusive; }
        site: Site;
        
        # When creating a plot, the surgeon access their own `SurgeonCas`es (which are 
        # restricted) but accesses others' `Cas`es (which are unrestricted) for comparison.
        access policy surgeon_full_access
            allow all using (.surgeon ?= global cur_surgeon) {
                errmessage := "Only the surgeon has access to their cases."
            };
    }

    type SurgeonDefaults extending SoftCreate {
        site: Site;
        iol: Iol;
        formula: Formula;
        required custom_constant: bool { default := false; }
        main: Main;
    }

    type SurgeonSia extending SoftCreate {
        required right: Sia;
        required left: Sia;
    }

    type Target extending SoftCreate {
        required formula: Formula;
        required custom_constant: bool { default := false; }
        
        required se: int32 {
            constraint min_value(-600);
            constraint max_value(200);
        }

        cyl: TargetCyl;
    }

    type TargetCyl extending Cyl {
        constraint expression on (.power >= 0 and .power <= 600);
    }

    type Va extending SoftCreate {
        required num: int32 { constraint min_value(0); constraint max_value(2000); }
        required den: int32 { constraint min_ex_value(0); }
    }
}
